description = "The gradle build for the JassTheRipper bot"
version = "1.0.0"

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'

mainClassName = "com.zuehlke.jasschallenge.Application"

def javaVersion = 1.8 // We have to use 1.8 because of the server

compileJava {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

task(runExperiment, dependsOn: 'classes', type: JavaExec) {
    main = 'com.zuehlke.jasschallenge.client.game.strategy.training.Arena'
    classpath = sourceSets.main.runtimeClasspath
    args 'CollectDataSet' // Retrieve with args[0]
    systemProperty 'simple.message', 'Hello ' // Retrieve with System.getProperty("simple.message")
}

repositories {
    mavenCentral()
}

dependencies {
    // Websocket Communication
    compile 'org.eclipse.jetty.websocket:websocket-client:9.3.4.RC0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.0'

    // Logging
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'ch.qos.logback:logback-classic:1.1.3'

    // DeepLearning4J
    compile 'org.deeplearning4j:deeplearning4j-core:1.0.0-beta4'
    compile 'org.deeplearning4j:deeplearning4j-modelimport:1.0.0-beta'
    // compile 'org.deeplearning4j:deeplearning4j-ui_2.11:1.0.0-beta4' // Does not work!
    compile 'org.nd4j:nd4j-native-platform:1.0.0-beta4' // for local machine
    compile 'org.nd4j:nd4j-cuda-9.2:1.0.0-beta4' // for server

    // Utilities
    compile 'com.google.guava:guava:27.1-jre'

    // Automated Benchmarks
    testCompile 'org.seleniumhq.selenium:selenium-java:3.141.59'

    // JSON Interpretation
    testCompile 'com.shazam:shazamcrest:0.11'

    // Tests
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'com.pholser:junit-quickcheck-core:0.5-alpha-3'
    
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

run {
    if (project.hasProperty("myargs")) {
        args(myargs.split(','))
    }
}

buildscript {
    ext.kotlin_version = '1.3.41'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = javaVersion
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = javaVersion
    }
}