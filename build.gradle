description = "The gradle build for the JassTheRipper bot"
version = "1.0.0"

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'

mainClassName = "to.joeli.jass.Application"

def javaVersion = 1.8 // We have to use 1.8 because of the server
def jerseyVersion = 2.29

compileJava {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

task(runExperiment, dependsOn: 'classes', type: JavaExec) {
    main = 'to.joeli.jass.client.strategy.training.Arena'
    classpath = sourceSets.main.runtimeClasspath
    args 'CollectDataSet' // Retrieve with args[0]
    systemProperty 'simple.message', 'Hello ' // Retrieve with System.getProperty("simple.message")
}

task(startServer, dependsOn: 'classes', type: JavaExec) {
    main = 'to.joeli.jass.client.rest.Server'
    classpath = sourceSets.main.runtimeClasspath
}

repositories {
    mavenCentral()
}

dependencies {
    // Websocket Communication
    compile 'org.eclipse.jetty.websocket:websocket-client:9.3.4.RC0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.0'

    // Logging
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'ch.qos.logback:logback-classic:1.1.3'

    // Tensorflow
    compile "org.tensorflow:tensorflow:1.13.1" // 1.14.0 does somehow not work!

    // CBOR
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.7.4'

    // JSON
    compile 'com.google.code.gson:gson:2.8.5'

    // Utilities
    compile 'com.google.guava:guava:27.1-jre'
    compile 'org.apache.commons:commons-lang3:3.9'
    compile 'commons-io:commons-io:2.6'

    // REST Endpoint
    compile "org.glassfish.jersey.containers:jersey-container-servlet:$jerseyVersion"
    compile "org.glassfish.jersey.core:jersey-client:$jerseyVersion"
    compile "org.glassfish.jersey.media:jersey-media-json-jackson:$jerseyVersion"
    compile "org.glassfish.jersey.containers:jersey-container-grizzly2-http:$jerseyVersion"
    compile "org.glassfish.jersey.inject:jersey-hk2:$jerseyVersion"

    // Automated Benchmarks
    testCompile 'org.seleniumhq.selenium:selenium-java:3.141.59'

    // JSON Interpretation
    testCompile 'com.shazam:shazamcrest:0.11'

    // Micro Benchmarks
    testCompile 'org.openjdk.jmh:jmh-core:1.21'
    testCompile 'org.openjdk.jmh:jmh-generator-annprocess:1.21'

    // Tests
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'com.pholser:junit-quickcheck-core:0.5-alpha-3'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

run {
    if (project.hasProperty("myargs")) {
        args(myargs.split(','))
    }
}

buildscript {
    ext.kotlin_version = '1.3.41'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = javaVersion
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = javaVersion
    }
}